#version 430 core

layout (local_size_x = 1, local_size_y = 1) in;

layout (binding = 0, rgba32f) writeonly uniform image2D normalMap;
layout (binding = 1, r16) readonly uniform image2D heightMap;

uniform float strength;

vec4 getCol(uint x, uint y) {
    return imageLoad(heightMap, ivec2(x, y));
}

float getHeight(uint x, uint y) {
    return getCol(x, y).r;
}

void main(void) {

	uint x = gl_GlobalInvocationID.x, y = gl_GlobalInvocationID.y;
	
	float z0 = getHeight(x - 1, y - 1);
    float z1 = getHeight(x, y - 1);
    float z2 = getHeight(x + 1, y - 1);
    float z3 = getHeight(x - 1, y);
    float z4 = getHeight(x + 1, y);
    float z5 = getHeight(x - 1, y + 1);
    float z6 = getHeight(x, y + 1);
    float z7 = getHeight(x + 1, y + 1);

    vec3 normal = vec3(0.0);
    normal.y = strength;
    normal.x = z0 + 2 * z3 + z5 - z2 - 2 * z4 - z7;
    normal.z = z0 + 2 * z1 + z2 - z5 - 2 * z6 - z7;
    
    imageStore(normalMap, ivec2(x, y), vec4(normalize(normal), 1.0));

}